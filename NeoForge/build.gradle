plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'io.github.goooler.shadow'
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'
}

archivesBaseName = "${mod_id}"
version = neoforge_mod_version

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':Common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_mc_version
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-neoforge', version: "${spectrelib_version}"
    jarJar group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-neoforge', version: "${spectrelib_version}"

    // Testing mods
    //runtimeOnly "curse.maven:configured-457570:5441232"
    //runtimeOnly "curse.maven:catalogue-459701:5631367"
    runtimeOnly "mezz.jei:jei-1.21-neoforge:${jei_version}"
    //runtimeOnly "curse.maven:farmers_delight-398521:${farmers_delight_forge_version}"
    //runtimeOnly "curse.maven:extra_delight-842106:5659546"
    //runtimeOnly "curse.maven:ephero_lib-885449:${ephero_lib_forge_version}"
    //runtimeOnly "curse.maven:croptopia-415438:${croptopia_forge_version}"
    //runtimeOnly "curse.maven:the_veggie_way-270684:${the_veggie_way_forge_version}"
    //runtimeOnly "curse.maven:phc_crops-361385:${phc_crops_forge_version}"
    //runtimeOnly "curse.maven:phc_trees-365460:${phc_trees_forge_version}"
    //runtimeOnly "curse.maven:kiwi-303657:${kiwi_forge_version}"
    //runtimeOnly "curse.maven:lychee-567403:${lychee_forge_version}"
    //runtimeOnly "curse.maven:fruittrees-355467:${fruit_trees_forge_version}"
}

shadowJar {
    archiveClassifier = ''
}

tasks.register('idePostSync') {}

task changelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File(project(":").projectDir.toString() + '/build/libs/changelog.txt');
    templateContent = file(project(":").projectDir.toString() + '/files/templates/changelog.mustache').getText('UTF-8');
    fromRepo = file("$projectDir");
    toRef = "HEAD";
    fromRef = "${minecraft_version}-${last_mod_version}";
}

def updates = tasks.register("generateUpdates", Copy) {
    it.outputs.upToDateWhen {
        false
    }

    it.from("../files/templates/") {
        include 'updates.json'
        expand 'version': version, 'minecraft_version': minecraft_version
    }

    it.into '../files/'
}

tasks.generateUpdates.mustRunAfter('jar', 'sourcesJar')

tasks.build.dependsOn('changelog', 'generateUpdates')

def changelogFile = project(":").projectDir.toString() + '/build/libs/changelog.txt'
def publishFile = project(":").projectDir.toString() + "/NeoForge/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    changelog = file(changelogFile).text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add('neoforge')

    if (project.hasProperty('ahcurseApiKey')) {
        curseforge {
            accessToken = project.ahcurseApiKey
            projectId = project.ahcurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
            optional('configured')
            optional('catalogue')
			optional('farmers-delight')
			optional('the-veggie-way')
			optional('pams-harvestcraft-2-crops')
			optional('pams-harvestcraft-2-trees')
			optional('fruit-trees')
			optional('croptopia')
        }
    }
    if (project.hasProperty('ahModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.ahModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
            optional('farmers-delight')
            optional('fruitful-fun')
        }
    }
    if (project.hasProperty('ahGithubToken')) {
    }
}
