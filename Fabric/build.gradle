plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

version = fabric_mod_version
archivesBaseName = "${mod_id}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_mc_version}:${parchment_version}@zip")
    })

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"
    include "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"

    modRuntimeOnly("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive = false
    }

    include group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-fabric', version: "${spectrelib_version}"

    // Testing Mods
    //modRuntimeOnly "curse.maven:epherolib-885449:${ephero_lib_fabric_version}"
    //modRuntimeOnly "curse.maven:croptopia-415438:${croptopia_fabric_version}"
}

loom {
    def aw = project(":Common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

def changelogFile = project(":").projectDir.toString() + '/build/libs/changelog.txt'
def publishFile = project(":").projectDir.toString() + "/Fabric/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    changelog = file(changelogFile).text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add('fabric')

    if (project.hasProperty('ahcurseApiKey')) {
        curseforge {
            accessToken = project.ahcurseApiKey
            projectId = project.ahcurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
			optional('farmers-delight-refabricated')
			optional('veggieway-fabric')
			optional('croptopia')
			optional('fruit-trees')
        }
    }
    if (project.hasProperty('ahModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.ahModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
            optional('farmers-delight-refabricated')
            optional('fruitful-fun')
        }
    }
    if (project.hasProperty('ahGithubToken')) {
    }
}
